plugins {
    id 'java-library'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.jvm'
    id 'org.jetbrains.kotlin.plugin.serialization'
    id 'signing'
}

repositories {
    mavenLocal()
    maven {
        url = 'https://jcenter.bintray.com'
    }

    maven {
        url = 'https://repo.maven.apache.org/maven2'
    }
}

dependencies {
    implementation 'io.cucumber:gherkin:9.0.0'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation 'org.jetbrains.kotlin:kotlin-reflect'
    implementation 'io.ktor:ktor-server-netty:1.3.1'
    implementation 'io.ktor:ktor-server-core:1.3.1'
    implementation 'io.ktor:ktor-client-core-jvm:1.3.1'
    implementation 'io.ktor:ktor-client-cio:1.3.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-runtime:0.20.0'
    implementation 'org.eclipse.jgit:org.eclipse.jgit:5.7.0.202003110725-r'
    implementation 'org.eclipse.jgit:org.eclipse.jgit.ssh.apache:5.7.0.202003110725-r'

    implementation 'org.apache.kafka:kafka_2.13:2.5.0'
    implementation 'org.apache.zookeeper:zookeeper:3.6.1'
    implementation 'io.dropwizard.metrics:metrics-core:4.1.7'

    implementation "org.testcontainers:testcontainers:1.14.1"
    implementation 'org.testcontainers:kafka:1.14.1'

    testRuntimeOnly('org.junit.jupiter:junit-jupiter-engine:5.4.2')

    testImplementation 'com.github.tomakehurst:wiremock-jre8:2.26.0'
    testImplementation 'org.assertj:assertj-core:3.12.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.4.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.5.2'
    testImplementation 'org.json:json:20190722'
    testImplementation 'com.intuit.karate:karate-core:0.9.4'
    testImplementation 'com.intuit.karate:karate-apache:0.9.4'
    testImplementation 'com.intuit.karate:karate-junit5:0.9.4'
    testImplementation 'org.springframework:spring-web:5.2.2.RELEASE'
    testImplementation 'org.jetbrains.kotlin:kotlin-test'
    testImplementation 'io.mockk:mockk:1.9.3.kotlin12'
    testImplementation 'org.assertj:assertj-core:3.14.0'
    testImplementation 'io.ktor:ktor-client-mock-jvm:1.3.1'
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

test {
    useJUnitPlatform()
}

group = 'run.qontract'

jar {
    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'qontract-core'
            from components.java
            pom {
                name = 'Qontract'
                description = 'A Contract Testing Tool that leverages Gherkin to describe APIs in a human readable and machine enforceable manner'
                url = 'https://qontract.run'
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://github.com/qontract/qontract/blob/master/License.md'
                    }
                }
                developers {
                    developer {
                        id = 'qontractBuilders'
                        name = 'Qontract Builders'
                        email = 'info@qontract.run'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:qontract/qontract.git'
                    url = 'https://qontract.run/'
                }
            }
        }
    }
    repositories {
        maven {
            // change URLs to point to your repos, e.g. http://my.org/repo
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = project.hasProperty("ossrhUsername") ? project.getProperty("ossrhUsername") : ""
                password = project.hasProperty("ossrhPassword") ? project.getProperty("ossrhPassword") : ""
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

tasks.withType(Sign) {
    onlyIf { project.hasProperty("signing.keyId") }
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

compileKotlin {
    kotlinOptions {
        freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
    }
}
